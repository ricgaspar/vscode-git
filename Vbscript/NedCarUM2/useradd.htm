<html>
<!--********************************************************************
'*
'*  File:           useradd.htm
'*  Created:        Augustus 2004
'*  Version:        1.0
'*  Author:         Marcel Jussen
'*
'*  Description:    NedCar user Management scripts.  
'*
'*  Copyright (C) 2004 KPN Telecom
'*
'********************************************************************-->
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1" />
<title>TEST</title>
<link href="css/nedcarum.css" rel="stylesheet" type="text/css" />

<SCRIPT Language="VBScript">
Option Explicit	

const ADS_PROPERTY_UPDATE = 2

sub cleartbNewUsers
  	do while tbNewUsers.Rows.Length > 0
    		tbNewUsers.deleteRow(tbNewUsers.Rows.Length - 1)
  	loop
end sub

sub cleartbCreateProgress
  	do while tbCreateProgress.Rows.Length > 0
    		tbCreateProgress.deleteRow(tbCreateProgress.Rows.Length - 1)
  	loop
end sub

sub clearTables
  	call cleartbNewUsers()
  	call cleartbCreateProgress()
end sub

sub clearForm
	UID.value = ""
	firstname.value = ""
	lastname.value = ""
	comment.value = ""
end sub

' function to list the properties of a user-account
Sub doAddAccountToTable
	Dim Usr, sError, sTemp
	Dim rowNew, cellNew
	dim strDomainName, strUserName
	Dim bUIDExists, strButton
	dim nCount, nPos, blInvalid
	
	' Capitalize the entire UID
	UID.value = Trim(UCase(UID.value))
	Firstname.value = Trim(Firstname.value)
	Lastname.value = Trim(Lastname.value)
	
	nCount = tbNewUsers.Rows.Length
	blInvalid = False
	for nPos = 0 to nCount-1
		strUserName = 	tbNewUsers.rows(nPos).cells(0).innertext
		if (strUserName = UID.value) and (blInvalid = False) then blInvalid = True
	next
	
	if (Len(UID.value) > 0) and (blInvalid=False) then
		bUIDExists = chkUIDExists(UID.value)		
		if bUIDExists = True then
			' this account already exists!
			call MsgBox("The account " & UID.value & " already exists in the domain " & strDomainName, 16, "Error") 
		else
			set rowNew = tbNewUsers.InsertRow()
			set cellNew = rowNew.InsertCell()
			cellnew.innerText = UID.value
		
			' Capitalize the first characters from first and lastname
			Firstname.value = UCase(Left(Firstname.value,1)) & Mid(Firstname.value, 2)
			Lastname.value = UCase(Left(Lastname.value,1)) & Mid(Lastname.value, 2)
			
			set cellNew = rowNew.InsertCell()
			cellnew.innerText = Firstname.value
				
			set cellNew = rowNew.InsertCell()
			cellnew.innerText = Lastname.value
		
			' Capitalize the entire comment field
			comment.value = Trim(UCase(comment.value))
			set cellNew = rowNew.InsertCell()
			cellnew.innerText = comment.value
			
		End if
	Else
		' Show an error message 
		if blInvalid then
			call MsgBox("User " & strUserName & " already exists in the table.", 16, "Error") 
			UID.value = ""
			call UID.focus()
		else
			call MsgBox("Please enter a valid userid!", 16, "Error") 
		end if
	End If
end sub

sub doChangeExternal
	if (chkExternal(0).checked) then comment.value = ""	
	if (chkExternal(1).checked) then comment.value = "NONNEDCAR"	
end sub

sub doCreateUsers
	Dim Usr, sError, sTemp
	Dim nCount, nPos, strDomainName
	Dim rowNew, cellNew
	Dim strUserName, strFirstname, strLastname, strComment
	
	call clearForm()
	call cleartbCreateProgress()	
	
	nCount = tbNewUsers.Rows.Length
	for nPos = 0 to nCount-1
		' collect stored information from DHTML table
		strUserName = 	tbNewUsers.rows(nPos).cells(0).innertext
		strFirstname = 	tbNewUsers.rows(nPos).cells(1).innertext
		strLastname = 	tbNewUsers.rows(nPos).cells(2).innertext
		strComment =	tbNewUsers.rows(nPos).cells(3).innertext
	
		' Show progress information.
		call ShowText("Creating account " & strUsername, "Add account to domain.")
		
		' Add log entry
		AddToLog("Add account " & strUserName & " to domain " & strDomainName)
		
		' Create account in the domain		
		if (chkUIDExists(strUserName) <> True) then
			
			call createUID(strUserName, strFirstname, strLastname, strComment)						
			call ShowText("Creating account " & strUsername, "Done.")			
			
			call ShowText("Creating account " & strUsername, "Adding groups to account.")					
			' Add account to default groups
			if InStr(UCase(strComment), "NONNEDCAR") <= 0 then
				' Default groups voor standard NEDCAR accounts
				call addUserToGroup(strUsername, "CitrixRollout")
				call addUserToGroup(strUsername, "CitrixGebruikers")
				call addUserToGroup(strUsername, "VNC")
				call addUserToGroup(strUsername, "CitrixNoUniPrint")			
				call ShowText("Creating account " & strUsername, "Added to Default NEDCAR domain groups.")
				' Add log entry
				AddToLog("Add account " & strUserName & " Added to Default NEDCAR domain groups.")
			else
				' Default groups for NONNEDCAR accounts
				call addUserToGroup(strUsername, "Non-NedCar")
				call addUserToGroup(strUsername, "CitrixCWI")			
				call ShowText("Creating account " & strUsername, "Added to NON-NEDCAR domain groups.")
				' Add log entry
				AddToLog("Add account " & strUserName & " Added to NON-NEDCAR domain groups.")
			end if
			call ShowText("Creating account " & strUsername, "Done.")			
			
			' Create a homedirectory and set rights.		
			call createHomeShare(strUserName)
					
			' Create a CitrixProfile and set rights.									
			call createCitrixProfile(strUserName)
			
			call MsgBox("Userid " & strUsername & " is nu gereed.", vbExclamation, "Info")	
			
			call cleartbCreateProgress()
			
		else
			call MsgBox("This account already exists in the domain " & strDomainName, 16, "Error") 
			cellnew.innerText = "ERROR: The account " & strUsername & " already exists in the domain " & strDomainName
			' Add log entry
			AddToLog("Add account " & strUserName & " ERROR: The account " & strUsername & " already exists in the domain " & strDomainName)
		end if		
		
		call ShowText("Creating account " & strUsername, "Account is ready.")
		
		' Add log entry
		AddToLog("Add account " & strUserName & " Ready adding account " & strUserName)		
	next
	
	call cleartbNewUsers()
end sub

'- FOLDERS and SHARES -------------------------------
'function to check if a folder exists
Function chkFolderExists(sFolder)
	dim fso, bResult
	set fso = CreateObject("Scripting.FileSystemObject")
	if isEmpty(sfolder) then
		bResult = False 
	else 
		bResult = fso.FolderExists(sFolder)
	end if
	chkFolderExists = bResult
End Function

Function createHomeShare(strUserName)
	dim strCommand, strParms
	Dim wshell, intReturn
	
	call ShowText("Creating account " & strUsername, "Creating home directory. Wait for procedure to finish.")
	
	strCommand = "createHomeShare.cmd " & strUserName
  set wshell = createobject("wscript.shell")
  intReturn = wshell.run("%comspec% /c " & strCommand, 0, True)
  
  call ShowText("Creating account " & strUsername, "Done.")
    	
  ' Add log entry
	AddToLog("Add account " & strUserName & " Create Home share")
End Function

Function createCitrixProfile(strUserName)
	Dim wshShell, strCommand, strRun, strUser
		
	call ShowText("Creating account " & strUsername, "Creating Citrix profile. Wait for procedure to finish.")
	 	
	' create shell and run command in RUNAS environment.
	set wshShell = CreateObject("WScript.Shell")	
	strCommand = "createCTXProfile.cmd " & strUsername 	
	dim intReturn
  intReturn = wshShell.run("%comspec% /c " & strCommand, 0, True)
	 
  call ShowText("Creating account " & strUsername, "Done.")
   	
   	' Add log entry
	AddToLog("Add account " & strUserName & " Create Citrix profile")
End Function

'- ADSI ----------------------------------------------

'function to check if a UID exists in the domain
Function chkUIDExists(strUserName)
	dim bResult
	bResult=False
	
	' On Error Resume Next
		
	dim objConnection
	Set objConnection = CreateObject("ADODB.Connection")
	objConnection.Open "Provider=ADsDSOObject;"
	
	'Get the ADsPath for the domain to search. 
	dim Root, sDomain
	Set Root = GetObject("LDAP://rootDSE")	
	sDomain = root.Get("rootDomainNamingContext")
	dim domain, sADSPath
	Set domain = GetObject("GC://" & sDomain)
	sADsPath = "<" & domain.ADsPath & ">"	
 
 	dim objCommand
	Set objCommand = CreateObject("ADODB.Command")
	objCommand.ActiveConnection = objConnection
 
	objCommand.CommandText = sADsPath & ";(&(objectCategory=User)" & _
         "(samAccountName=" & strUserName & "));samAccountName;subtree"
  
  dim objRecordSet
	Set objRecordSet = objCommand.Execute
 
	If objRecordset.RecordCount = 0 Then
  	bResult=False
	Else
    bResult=True
	End If
 
	objConnection.Close
	chkUIDExists = bResult
End Function


Sub createUID(strUsername, strFirstname, strLastname, strComment)
	' On Error Resume Next
	
	dim objConnection
	Set objConnection = CreateObject("ADODB.Connection")
	objConnection.Open "Provider=ADsDSOObject;"
	
	dim Root, sDomain
	Set Root = GetObject("LDAP://rootDSE")	
	sDomain = root.Get("rootDomainNamingContext")
	dim domain, sADSPath
	Set domain = GetObject("LDAP://cn=Users," & sDomain)
	sADsPath = domain.ADsPath
 
  dim objOU, objUser
	Set objOU = GetObject(sADsPath)
	Set objUser = objOU.Create("User", "cn=" & strUserName)
	objUser.Put "sAMAccountName", strUserName
	objUser.SetInfo

	if err.number<0 then
		call MsgBox("Could not create user " & strUserName & " in domain " & sDomain, 16, "Error") 
		' Add log entry
		AddToLog("Add account " & strUserName & " ERROR: Could not create user " & strUserName)
		err.Clear
	else 
		' Set account properties		
		' General properties
		'-------------------------------------
		if len(strFirstname)>0 then objUser.Put "givenName", strFirstname
		' objUser.Put "initials", "E."
		if len(strLastname)>0 then objUser.Put "sn", strLastname
		
		dim strFullname
		strFullname = strLastname & ", " & strFirstname		
		
		if len(strFullname) > 0 then objUser.Put "displayName", strFullname
		if len(strComment) > 0 then objUser.PutEx ADS_PROPERTY_UPDATE, "description", Array(strComment)
		if len(strComment) > 0 then objUser.Put "department", strComment
		
		' objUser.Put "physicalDeliveryOfficeName", "Room 4358" 
		' objUser.Put "telephoneNumber", "(425) 555-1211"
		' objUser.Put "mail", "myerken@fabrikam.com"
		objUser.Put "wWWHomePage", "http://intranet.nedcar.nl"  
		
		' objUser.PutEx ADS_PROPERTY_UPDATE, "otherTelephone", Array("(800) 555-1212", "(425) 555-1213")  
		' objUser.PutEx ADS_PROPERTY_UPDATE, "url", Array("http://www.fabrikam.com/management")
		objUser.SetInfo
		
		'Profile properties
		'-------------------------------------
		objUser.Put "scriptPath", "logon.bat"		
		' objUser.Put "profilePath", ""
		' objUser.Put "homeDirectory", "\\S034\" & strUsername & "$"
		' objUser.Put "homeDrive", "U:"
		objUser.SetInfo	
	
		' Set default password en make sure to change password at next logon.
		objUser.SetPassword "nedcar"
		objUser.SetInfo
		
		' Require user to change password
		objUser.Put "pwdLastSet", 0
		objUser.SetInfo
		
		' Enable the user account
		objUser.AccountDisabled = FALSE
		objUser.SetInfo
		
		' Terminal Services properties
		objUser.TerminalServicesProfilePath = "\\s060\ctxprof$\" & strUserName
		objUser.TerminalServicesHomeDirectory = ""
		objUser.TerminalServicesHomeDrive = ""
		objUser.AllowLogon = 1
		objUser.SetInfo
			
		' Add log entry
		AddToLog("Add account " & strUserName & " Account properties succesfully set.")
		
	end If
		
	' Done so clear variables
	Set objUser = Nothing
	
End Sub

Sub addUserToGroup(strUsername, strGroupName)
	Const ADS_PROPERTY_APPEND = 3
	
	dim objConnection
	Set objConnection = CreateObject("ADODB.Connection")
	objConnection.Open "Provider=ADsDSOObject;"
	
	dim Root, sDomain
	Set Root = GetObject("LDAP://rootDSE")	
	sDomain = root.Get("rootDomainNamingContext")
	
	dim objGroup
	Set objGroup = GetObject("LDAP://cn=" & strGroupname & ",cn=Users," & sDomain)	
	objGroup.PutEx ADS_PROPERTY_APPEND, "member", Array("cn=" & strUsername & ",cn=Users," & sDomain)
	objGroup.SetInfo

End Sub

sub doCheckUID
	dim strDomainName, strUserName, blInvalid
	dim nCount, nPos
	
	UID.value = UCase(Trim(UID.value))
	strUserName = UID.value
	
	if chkUIDExists(strUserName) then
		call MsgBox("User " & strUserName & " already exists in the domain.", 16, "Error") 
		UID.value = ""
		call UID.focus()
	else
		nCount = tbNewUsers.Rows.Length
		blInvalid = False
		for nPos = 0 to nCount-1
			strUserName = 	tbNewUsers.rows(nPos).cells(0).innertext
			if (strUserName = UID.value) and (blInvalid = False) then blInvalid = True
		next
		if blInvalid then
			call MsgBox("User " & strUserName & " already exists in the table.", 16, "Error") 
			UID.value = ""
			call UID.focus()
		end if
	end if
	
end Sub

Sub ShowText(strTextLeft, strTextRight)
	dim rowNew, cellNew
	set rowNew = tbCreateProgress.InsertRow()
	set cellNew = rowNew.InsertCell()
	cellnew.innerText = strTextLeft
	set cellNew = rowNew.InsertCell()
	cellnew.innerText = strTextRight
End Sub

Sub AddToLog(strText)
	const	ForAppend 	= 8
	const	LogFile		= "\\S100\LOGBOEK\NEDCARUM2\nedcarum2.log"
	const	htmSource	= "UserAdd"
	
	dim WshNetwork, fsoObj, f, strTemp
	dim strDomain, strComputer, strUsername
	dim strTime
	
	Set WshNetwork = CreateObject("WScript.Network")
	strComputer = WshNetwork.ComputerName
        strUsername = WshNetwork.UserName
	strTime = Now()
	
	Set fsoObj = CreateObject("Scripting.FileSystemObject")
   	Set f = fsoObj.OpenTextFile(LogFile, ForAppend, True)
   	strTemp = strTime & Chr(32) & htmSource & ", " & strComputer & ", " & strUsername & ", " & strText
   	f.WriteLine strTemp
   	f.Close
End Sub

'--------------------------------------------------------

</SCRIPT>
<style type="text/css">
<!--
.style1 {color: #FFFFCC}
-->
</style>
</head><body class="nedcarum">
<strong>User Add.</strong><br>
<P>Add users to domain.</P>
<table width="100%" border="1" cellspacing="0" cellpadding="0"><tr><th scope="row">
<table width="100%" border=0 align="left" cellpadding="0" cellspacing="0">
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;User ID </div></th>
      <td width="300"><P>
         <INPUT name="UID" type="text" class="nedcarumform" id="UID" size="25" maxlength="25"
         	title="Type the unique new user account ID."
         	onchange="doCheckUID">
         </P></td>
      <td>&nbsp;</td>
    </tr>
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;Firstname</div></th>
      <td width="300"><P>
      	<INPUT name="firstname" type="text" class="nedcarumform" id="firstname" size="30" maxlength="30"
       		title="Type the firstname of this account.">
       	</P></td>
      <td>&nbsp;</td>
    </tr>
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;Lastname</div></th>
      <td width="300"><P>
      	<INPUT name="lastname" type="text" class="nedcarumform" id="lastname" size="30" maxlength="30"
       		title="Type the lastname of this account.">
       	</P></td>
      <td>&nbsp;</td>
    </tr>
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;Comment/Department</div></th>
      <td width="300"><P>
      	<INPUT name="comment" type="text" class="nedcarumform" id="comment" size="30" maxlength="30"
      		title="Type the department number or a usefull comment.">
      	</P>
      <td>
      	<P>
      	<INPUT type="radio" name="idtype" id=chkExternal onclick="doChangeExternal" 
      		title="Select this for a standard NedCar account"
      		CHECKED>NedCar default account.
      	</P></td>
      </td>
    </tr>
    <tr>
      <th width="25%" scope="row">&nbsp;</th>
      <td width="300">&nbsp;</td>
      <td><input type="radio" name="idtype" id=chkExternal onclick="doChangeExternal"
      		title="Select this for an External account">Non NedCar account.</td>
    </tr>
    <tr>
      <th width="25%" scope="row">&nbsp;</th>
      <td width="300">
	  <P>
	    <input name="AddToTable" type="button" id=addtotable title="Add the information to the users table." onclick="doAddAccountToTable" value="Add to table">
	     
    	<input name="ClearForm" type="reset" id="clearform" title="Clear the input form." onclick="clearForm" value="Clear form">
    	 
	  </P>
      <td>&nbsp;</td>
    </tr>
</table>

</th>
  </tr>
</table>
<BR>
<table width="100%"  border="1" cellspacing="0" cellpadding="0">
  <tr><th scope="row" align="left">
<table width="100%" border="1" cellpadding="0" cellspacing="0" class="nedcarum">
  <THEAD>
  <TR bgcolor="#000099">
    <TH width="25%" align="left"><span class="style1"><strong>&nbsp;User ID</strong></span></TH>
    <TH align="left"><span class="style1"><strong>&nbsp;Firstname</strong></span></TH>
    <TH align="left"><span class="style1"><strong>&nbsp;Lastname</strong></span></TH>
    <TH align="left"><span class="style1"><strong>&nbsp;Comment/Department</strong></span></TH>
  </TR>
  </THEAD>
  <TBODY ID = "tbNewUsers">
  </TBODY>
</TABLE>
<br>
<P>
  <input = name="AddToTable" type ="BUTTON" id="createusers"
  	title="Press this button to start creating the new accounts." onclick="doCreateUsers" value="Create users">
  &nbsp;
  <input name="ClearTable" type = "BUTTON" id="cleartable"
  	title="Clear the new users table." onclick="clearTables" value="Clear table">
</P>
</th></tr>
</table>
<BR>
<table width="100%"  border="1" cellspacing="0" cellpadding="0">
  <tr><th scope="row" align="left">

<table width="100%" border="1" cellpadding="0" cellspacing="0">
  <THEAD>
  <TR bgcolor = "#000099">
    <TH width="25%" align="left"><span class="style1"><strong>&nbsp;Action</strong></span></TH>
    <TH width="75%" align="left"><span class="style1"><strong>&nbsp;Result</strong></span></TH>
  </TR>
  </THEAD>
  <TBODY ID = "tbCreateProgress">
  </TBODY>
</TABLE>
</th></tr>
</table>

</body>
</html>
