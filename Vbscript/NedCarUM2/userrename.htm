<html>
<!--********************************************************************
'*
'*  File:           userrename.htm
'*  Created:        March 2006
'*  Version:        1.0
'*  Author:         Marcel Jussen
'*
'*  Description:    NedCar user Management scripts.
'*
'*  Copyright (C) 2006 KPN Telecom
'*
'********************************************************************-->
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1" />
<title>TEST</title>
<link href="css/nedcarum.css" rel="stylesheet" type="text/css" />

<SCRIPT Language="VBScript">
Option Explicit

const CLR_BLACK = &H000000
const CLR_RED 	= &HFF0000
const CLR_GREEN = &H00FF00

const ADS_PROPERTY_UPDATE = 2

sub cleartbRenameProgress
  	do while tbRenameProgress.Rows.Length > 0
    		tbRenameProgress.deleteRow(tbRenameProgress.Rows.Length - 1)
  	loop
end sub

sub clearTables
  	call cleartbRenameProgress()
end sub

sub clearForm
	UID.value = ""
	UID.style.color = CLR_BLACK
	NEWUID.value = ""
	NEWUID.style.color = CLR_BLACK
	call clearTables()
end sub

sub doChangeExternal
	if (chkExternal(0).checked) then comment.value = ""
	if (chkExternal(1).checked) then comment.value = "NONNEDCAR"
end sub

sub doRenameUser
	Dim Usr, sError, sTemp, nButton
	Dim nCount, nPos, strDomainName
	Dim bProcFailure, bUsrCheckFailure, bRes
	Dim strUserName, strNewUserName
	dim strHomeFolder, strHomeServer, strHomeShare

	call cleartbRenameProgress()

	strUserName = UID.value
	strNewUserName = UCase(trim(NEWUID.value))

	' Add log entry
	call ShowText("Renaming account " & strUserName, "New name is " & strNewUserName)
	AddToLog("Renaming account " & strUserName & " to " & strNewUserName)

	strHomeServer = "S034"
	bProcFailure = False
	bUsrCheckFailure = False

	' Create account in the domain
	if (chkUIDExists(strUserName) = True) then
		if (chkUIDExists(strNewUserName) = False) then

			NEWUID.value = strNewUserName
			NEWUID.style.color = CLR_RED

			' Check if old home folder exists
			'-------------------------------------------
			strHomeFolder = "\\S034\Data$\" & strUserName
			bRes = chkFolderExists(strHomeFolder)
			bProcFailure = not bRes
			bUsrCheckFailure = bProcFailure
			if bProcFailure then call MsgBox("The home folder " & strHomeFolder & " does not exist!", 16, "Error")

			if not bProcFailure then
				' Check if home folder is shared
				'-------------------------------------------
				strHomeShare = strUserName & "$"
				bRes = chkShareExists(strHomeServer, strHomeShare)
				bProcFailure = not bRes
				bUsrCheckFailure = bProcFailure
				
				if bProcFailure then call MsgBox("The share " & strHomeServer & "\" & strHomeShare & " does not exist!", 16, "Error")
			end if

			if not bProcFailure then UID.style.color = CLR_GREEN

			if not bProcFailure then
				' Check if new home folder exists
				'-------------------------------------------
				strHomeFolder = "\\S034\Data$\" & strNewUserName
				bRes = chkFolderExists(strHomeFolder)
				bProcFailure = bRes
				bUsrCheckFailure = bProcFailure
				if bProcFailure then call MsgBox("The new home folder " & strHomeFolder & " already exists!", 16, "Error")
			end if

			if not bProcFailure then
				' Check if new home folder is shared
				'-------------------------------------------
				strHomeShare = strNewUserName & "$"
				bRes = chkShareExists(strHomeServer, strHomeShare)
				bProcFailure = bRes
				bUsrCheckFailure = bProcFailure
				if bProcFailure then call MsgBox("The share " & strHomeServer & "\" & strHomeShare & " already exists!", 16, "Error")
			end if

			if not bProcFailure then NEWUID.style.color = CLR_GREEN
			call ShowText("Renaming account " & strUsername, "Prerequisite check ended.")

			if not bProcFailure then nButton = MsgBox("Are you sure you wish to rename the account " & strUserName & " to " & strNewUserName & " ?", vbYesNo + vbQuestion + vbDefaultButton2, "Commit changes")

			dim blCommitChanges
			blCommitChanges = (nButton <> vbNo) and (not bProcFailure)
			if blCommitChanges then
				NEWUID.style.color = CLR_GREEN
				' Remove share from home
				'----------------------------------
				call RemoveHomeShare(strUserName)

				' Rename home folder
				'----------------------------------
				call RenameHomeFolder(strUserName, strNewUserName)

				' Rename Citrix folder
				'----------------------------------
				call RenameCtxFolder(strUserName, strNewUserName)

				' Set account properties
				'----------------------------------
				call setAccountProps(strUserName, strNewUserName)

				' Rename User account
				'----------------------------------
				call renameUID(strUserName, strNewUserName)

				' Recreate home share
				'----------------------------------
				call createHomeShare(strNewUserName)

			end if
		else
			call MsgBox("The account " & strNewUserName & " already exists in the domain.", 16, "Error")
			bProcFailure=True
	  end if
	else
		call MsgBox("The account " & strUserName & " does not exist in the domain", 16, "Error")
		bProcFailure=True		
	end if

	if bProcFailure then
		call ShowText("Renaming account " & strUsername, "Process rename ended in failure.")
		call MsgBox("Process rename user account " & strUsername & " to " & strNewUserName & " ended in failure.", 16, "Info")		
	else
		call ShowText("Renaming account " & strUsername, "Process rename account is ready.")
		call MsgBox("Process rename user account " & strUsername & " to " & strNewUserName & " is done.", vbExclamation, "Info")
	end if
	
	if bUsrCheckFailure then
		call MsgBox("Run a usercheck on account " & strUsername & " before a rename is started!", vbExclamation, "Info")
	End if

	AddToLog("Renaming account " & strUsername & ". Process rename account is ready.")

end sub

Function RemoveHomeShare(strUserName)
	dim strCommand, strParms
	Dim wshell, intReturn

	call ShowText("Renaming account " & strUsername, "Remove share from current folder. Wait for procedure to finish.")

	strCommand = "removeHomeShare.cmd " & strUserName
  set wshell = createobject("wscript.shell")
  AddToLog(strCommand)
  intReturn = wshell.run("%comspec% /c " & strCommand, 0, True)
  AddToLog("Return value: " & CStr(intReturn))

  call ShowText("Renaming account " & strUsername, "Old share is removed")
	AddToLog("Renaming account " & strUserName & " Remove old home share.")
End Function

Function RenameHomeFolder(strUserName, strNewUserName)
	dim strCommand, strParms
	Dim wshell, intReturn

	call ShowText("Renaming account " & strUsername, "Rename folder to new account. Wait for procedure to finish.")

	strCommand = "renameHomeFolder.cmd " & strUserName & " " & strNewUserName
	AddToLog(strCommand)
  set wshell = createobject("wscript.shell")
  intReturn = wshell.run("%comspec% /c " & strCommand, 0, True)
  AddToLog("Return value: " & CStr(intReturn))

  call ShowText("Renaming account " & strUsername, "Home folder is renamed.")
	AddToLog("Renaming account " & strUserName & " Home folder is renamed.")
End Function

Function RenameCtxFolder(strUserName, strNewUserName)
	dim strCommand, strParms
	Dim wshell, intReturn

	call ShowText("Renaming account " & strUsername, "Rename Citrix profile folder to new account. Wait for procedure to finish.")

	strCommand = "renameCtxFolder.cmd " & strUserName & " " & strNewUserName
	AddToLog(strCommand)
  set wshell = createobject("wscript.shell")
  intReturn = wshell.run("%comspec% /c " & strCommand, 0, True)
  AddToLog("Return value: " & CStr(intReturn))

  call ShowText("Renaming account " & strUsername, "Citrix profile folder is renamed.")
	AddToLog("Renaming account " & strUserName & " Citrix profile folder is renamed.")
End Function

Function createHomeShare(strUserName)
	dim strCommand, strParms
	Dim wshell, intReturn

	call ShowText("Renaming account " & strUsername, "Recreating home directory. Wait for procedure to finish.")
	strCommand = "createHomeShare.cmd " & strUserName
  set wshell = createobject("wscript.shell")
  intReturn = wshell.run("%comspec% /c " & strCommand, 0, True)
  call ShowText("Renaming account " & strUsername, "Homedir U: done.")

  ' Add log entry
	AddToLog("Add account " & strUserName & " Create Home share")
End Function

'- FOLDERS and SHARES -------------------------------
'function to check if a folder exists
Function chkFolderExists(sFolder)
	dim fso, bResult
	set fso = CreateObject("Scripting.FileSystemObject")
	if isEmpty(sfolder) then
		bResult = False
	else
		bResult = fso.FolderExists(sFolder)
	end if
	chkFolderExists = bResult
End Function

'- ADSI ----------------------------------------------
Function renameUID(strUserName, strNewUserName)

	Dim strOldLDAP, strNewLDAP

	strOldLDAP="LDAP://cn=" & Trim(strUserName) & ",cn=users,dc=nedcar,dc=nl"
	strNewLDAP="LDAP://cn=" & Trim(strNewUserName) & ",cn=users,dc=nedcar,dc=nl"

	'Check if old accountname exists
	On Error Resume Next
	err.clear()

	call ShowText("Renaming account " & strUsername, "ADSI Change.")
	AddToLog(strOldLDAP)
	AddToLog(strNewLDAP)

	Dim objUser, objOU, intError
	Set objUser = GetObject(strOldLDAP)
	If err.number<>0 Then
		AddToLog("ERROR: Cannot bind to " & strOldLDAP)
		call ShowText("ERROR: Cannot bind to " & strOldLDAP)
	Else
		err.clear()

		'Check if new account not already exists.
		Set objUser = GetObject(strNewLDAP)
		If err.number=0 Then
			AddToLog("ERROR: Bind to " & strOldLDAP & " was successfull. Container should not exist!")
			call ShowText("ERROR: Bind to " & strOldLDAP & " was successfull.","Container should not exist!")
		Else
			' Bind to Users container
			Set objOU = GetObject("LDAP://CN=Users,DC=nedcar,DC=nl")
			call objOU.MoveHere(strOldLDAP, "cn=" & Trim(strNewUserName))
			addToLog("Renaming account in AD processed ended.")
		End If
	End If

End Function

function setAccountProps(strUserName, strNewUserName)
	dim Usr, sError, strDN, objUser
	strDN=QueryAD_User(strUserName)
	AddToLog("DN: " & strDN)

	call ShowText("Renaming account " & strUsername, "Setting account properties.")

	' On Error Resume Next
	Set objUser = GetObject("LDAP://" & strDN)
	objUser.Put "sAMAccountName", strNewUserName
	objUser.SetInfo

	addToLog("SAM account name set for account " & strDN & " to " & strNewUserName)

	' Terminal Services properties
	objUser.TerminalServicesProfilePath = "\\s060\ctxprof$\" & strNewUserName
	objUser.TerminalServicesHomeDirectory = ""
	objUser.TerminalServicesHomeDrive = ""
	objUser.AllowLogon = 1
	objUser.SetInfo

	addToLog("TS Profile settings set for account " & strDN)

End function

'function to check if a UID exists in the domain
Function chkUIDExists(strUserName)
	chkUIDExists = Len(QueryAd_User(strUserName)) > 0
End Function

Function QueryAD_User(strUserName)
	Dim Con, Root, Domain, sDomain, sFilter, sAttribsToReturn, sDepth
	Dim rs, i, Path
	Dim oCommand, sADsPath
	Dim objName, objClass, objSchema
	Dim classObject

	On Error Resume Next

	'--------------------------------------------------------
	'Create the ADO connection object
	'--------------------------------------------------------
	Set Con = CreateObject("ADODB.Connection")
	Con.Provider = "ADsDSOObject"
	Con.Open "Active Directory Provider"

	'Create ADO command object for the connection.
	Set oCommand = CreateObject("ADODB.Command")
	oCommand.ActiveConnection = Con

	'Get the ADsPath for the domain to search.
	Set Root = GetObject("LDAP://rootDSE")

	'---------------------------------------------------------
	'Choose the NC you want to search and build the ADsPath
	'---------------------------------------------------------
	sDomain = root.Get("rootDomainNamingContext")
	Set domain = GetObject("GC://" & sDomain)
	sADsPath = "<" & domain.ADsPath & ">"

	'--------------------------------------------------------
	'Build the search filter
	'--------------------------------------------------------
	sFilter = "(&(objectCategory=person)(objectClass=user)(Name=" & strUserName & "))"
  sAttribsToReturn = "distinguishedName"
	sDepth = "subtree"

	'---------------------------------------------------------
	'Assemble and execute the query
	'---------------------------------------------------------
	oCommand.CommandText = sADsPath & ";" & sFilter & ";" & sAttribsToReturn & ";" & sDepth
	Set rs = oCommand.Execute

	'---------------------------------------------------------
	' Navigate the record set and get the object's DN
	'---------------------------------------------------------
	rs.MoveFirst
	While Not rs.EOF
    For i = 0 To rs.Fields.Count - 1
    	If rs.Fields(i).Name = "distinguishedName" Then
	    	Path = rs.Fields(i).Value
      End If
    Next
    rs.MoveNext
	Wend

	AddToLog("Query result:" & Path)

	QueryAD_User = Path
End Function

sub doCheckUID
	dim strUserName, strTemp

	cleartbRenameProgress()
	UID.value = UCase(Trim(UID.value))
	strUserName = UID.value
	strTemp = QueryAD_User(strUserName)
	
	NEWUID.style.color = CLR_BLACK

	if chkUIDExists(strUserName) then
		NEWUID.value = ""
		UID.style.color = CLR_RED		
	else
		call MsgBox("User " & strUserName & " does not exist in the domain.", 16, "Error")
		UID.value = ""
		call UID.focus()
	end if

end Sub

sub doCheckNewUID	
	NEWUID.value = UCase(Trim(NEWUID.value))
end Sub

Sub ShowText(strTextLeft, strTextRight)
	dim rowNew, cellNew
	set rowNew = tbRenameProgress.InsertRow()
	set cellNew = rowNew.InsertCell()
	cellnew.innerText = strTextLeft
	set cellNew = rowNew.InsertCell()
	cellnew.innerText = strTextRight
End Sub

'- SHARES ----------------------------------------------
'function to check if a share exists (calls chkFolderExists)
Function chkShareExists(strServer, strShare)
	dim objShare, blResult, ShareName, share
	dim strDomain

	strDomain = QueryNet_Domain()
	Set objShare= GetOBJect("WinNT://"& strDomain &"/" & strServer &"/lanmanserver")

	' Fill the temp file with share information
	blResult = False
	For each share in objShare
		ShareName = share.name
		' AddToLog(ShareName)
		if (blResult=False) AND (strShare=ShareName) then blResult=True
	Next
	chkShareExists = blResult
End Function

Function QueryNet_Domain()
	dim WshNetwork
	Set WshNetwork = CreateObject("WScript.Network")
	QueryNet_Domain = WshNetwork.UserDomain
End Function

Sub AddToLog(strText)
	const	ForAppend 	= 8
	const	htmSource	= "Rename"

	dim WshNetwork, fsoObj, f, strTemp
	dim strDomain, strComputer, strUsername
	dim strTime

	Set WshNetwork = CreateObject("WScript.Network")
	strComputer = WshNetwork.ComputerName
  strUsername = WshNetwork.UserName
	strTime = Now()

	dim WshShell, wshSysEnv, strLogFile
	Set WshShell = CreateObject("WScript.Shell")
	Set WshSysEnv = WshShell.Environment("PROCESS")
	strLogFile = WshSysEnv("NEDCARUM2LOG")

	Set fsoObj = CreateObject("Scripting.FileSystemObject")
 	Set f = fsoObj.OpenTextFile(strLogFile, ForAppend, True)
 	strTemp = strTime & Chr(32) & htmSource & ", " & strComputer & ", " & strUsername & ", " & strText
 	f.WriteLine strTemp
 	f.Close
End Sub

'--------------------------------------------------------

</SCRIPT>
<style type="text/css">
<!--
.style1 {color: #FFFFCC}
-->
</style>
</head><body class="nedcarum">
<strong>User Rename.</strong><br>
<P>Rename existing users in the domain .</P>
<table width="100%" border="1" cellspacing="0" cellpadding="0"><tr><th scope="row">
<table width="100%" border=0 align="left" cellpadding="0" cellspacing="0">
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;Current account name</div></th>
      <td width="300"><P>
         <INPUT name="UID" type="text" class="nedcarumform" id="UID" size="25" maxlength="25"
         	title="Type the unique user account ID."
         	onchange="doCheckUID">
         </P></td>
      <td>&nbsp;</td>
    </tr>
    <tr>
      <th width="25%" scope="row"><div align="left" class="nedcarum">&nbsp;New account name </div></th>
      <td width="300"><P>
      	<INPUT name="NewUID" type="text" class="nedcarumform" id="NewUID" size="30" maxlength="30"
       		title="Type the firstname of this account."
       		onchange="doCheckNewUID">
       	</P></td>
      <td>&nbsp;</td>
    </tr>
    <tr>
      <th width="25%" scope="row">&nbsp;</th>
      <td width="300">
	  <P>
	    <input name="RenameUser" type="button" id=RenameUser title="Click to rename the user" onclick="doRenameUser" value="Rename account">
    	<input name="ClearForm" type="reset" id="clearform" title="Clear the input form." onclick="clearForm" value="Clear form">    	 
	  </P>
      <td>&nbsp;</td>
    </tr>
</table>

</th>
  </tr>
</table>
<BR>
<table width="100%"  border="1" cellspacing="0" cellpadding="0">
  <tr><th scope="row" align="left">

<table width="100%" border="1" cellpadding="0" cellspacing="0">
  <THEAD>
  <TR bgcolor = "#000099">
    <TH width="25%" align="left"><span class="style1"><strong>&nbsp;Action</strong></span></TH>
    <TH width="75%" align="left"><span class="style1"><strong>&nbsp;Result</strong></span></TH>
  </TR>
  </THEAD>
  <TBODY ID = "tbRenameProgress">
  </TBODY>
</TABLE>
</th></tr>
</table>

</body>
</html>
